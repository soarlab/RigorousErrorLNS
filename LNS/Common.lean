import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv

attribute [fun_prop] Differentiable.rpow
attribute [fun_prop] Differentiable.exp
attribute [fun_prop] Differentiable.div
attribute [fun_prop] DifferentiableOn.rpow
attribute [fun_prop] DifferentiableOn.exp
attribute [fun_prop] DifferentiableOn.div

attribute [fun_prop] ContinuousOn.div_const
attribute [fun_prop] ContinuousOn.rpow
attribute [fun_prop] ContinuousOn.div

-- TODO: generalize, simplify and add to Mathlib

lemma List.forall_append {Î± : Type u} (xs ys : List Î±) (p : Î± â†’ Prop) :
    (xs ++ ys).Forall p â†” xs.Forall p âˆ§ ys.Forall p := by
  repeat rw [List.forall_iff_forall_mem]
  rw [List.forall_mem_append]

/- Derivatives for compositions with a linear transformation -/

lemma DifferentiableAt.comp_linear {a b x : â„} {f : â„ â†’ â„} (ha : a â‰  0) :
    DifferentiableAt â„ (fun x => f (a * x + b)) x â†” DifferentiableAt â„ f (a * x + b) := by
  constructor <;> intro df
  Â· have : f = (fun x => f (a * x + b)) âˆ˜ (fun x => (x - b) / a) := by
      ext y; congr; field_simp
    rw [this]
    apply DifferentiableAt.comp
    Â· simp only [add_sub_cancel_right]; field_simp
      exact df
    Â· simp only [differentiableAt_id', differentiableAt_const, sub, div_const]
  Â· apply DifferentiableAt.comp
    Â· exact df
    Â· simp only [differentiableAt_add_const_iff]
      exact DifferentiableAt.const_mul differentiableAt_id' _

lemma deriv_comp_linear {a b x : â„} {f : â„ â†’ â„} :
    deriv (fun x => f (a * x + b)) x = a * deriv f (a * x + b) := by
  by_cases ha : a = 0; simp [ha]
  by_cases df : DifferentiableAt â„ f (a * x + b)
  Â· rw [â† Function.comp_def, deriv_comp, deriv_add_const, deriv_const_mul, deriv_id'', mul_comm, mul_one]
    Â· exact differentiableAt_id'
    Â· exact df
    Â· apply DifferentiableAt.add_const
      exact DifferentiableAt.const_mul differentiableAt_id' _
  Â· rw [deriv_zero_of_not_differentiableAt df, deriv_zero_of_not_differentiableAt, mul_zero]
    rw [DifferentiableAt.comp_linear ha]
    exact df

lemma DifferentiableAt.comp_const_sub {a x : â„} {f : â„ â†’ â„} :
    DifferentiableAt â„ (fun x => f (a - x)) x â†” DifferentiableAt â„ f (a - x) := by
  have : âˆ€ x, a - x = (-1) * x + a := by intro; ring
  simp only [this, DifferentiableAt.comp_linear (by norm_num : -1 â‰  (0 : â„))]

lemma DifferentiableAt.comp_sub_const {a x : â„} {f : â„ â†’ â„} :
    DifferentiableAt â„ (fun x => f (x - a)) x â†” DifferentiableAt â„ f (x - a) := by
  have : âˆ€ x, x - a = 1 * x + -a := by intro; ring
  simp only [this, DifferentiableAt.comp_linear (by norm_num : 1 â‰  (0 : â„))]

section Diff

open Real

lemma deriv_EqOn_open {f1 f2 : â„ â†’ â„} (hs : IsOpen s) (h : Set.EqOn f1 f2 s) (hx : x âˆˆ s) :
    deriv f1 x = deriv f2 x := by
  apply Filter.EventuallyEq.deriv_eq
  apply Filter.eventuallyEq_of_mem _ h
  exact IsOpen.mem_nhds hs hx

lemma deriv_EqOn_Iio {f1 f2: â„ â†’ â„} (h: Set.EqOn f1 f2 (Set.Iio (a:â„))) (hx: x âˆˆ Set.Iio (a:â„)) :
    deriv f1 x = deriv f2 x := deriv_EqOn_open isOpen_Iio h hx

lemma deriv_EqOn_Ioi {f1 f2: â„ â†’ â„} (h: Set.EqOn f1 f2 (Set.Ioi (a:â„))) (hx: x âˆˆ (Set.Ioi (a:â„))) :
    deriv f1 x = deriv f2 x := deriv_EqOn_open isOpen_Ioi h hx

lemma deriv_EqOn_Ioo {f1 f2: â„ â†’ â„} (h: Set.EqOn f1 f2 (Set.Ioo (a:â„) (b:â„))) (hx: x âˆˆ (Set.Ioo (a:â„) (b:â„))) :
    deriv f1 x = deriv f2 x := deriv_EqOn_open isOpen_Ioo h hx

/- Special derivatives -/

-- lemma HasDerivAt.const_rpow {f : â„ â†’ â„} {f' a : â„} (ha : 0 < a) (hf : HasDerivAt f f' x) :
--     HasDerivAt (fun x => a ^ f x) (f' * a ^ f x * Real.log a) x := by
--   rw [(by norm_num : f' * a ^ f x * Real.log a = 0 * f x * a ^ (f x - 1) + f' * a ^ f x * Real.log a)]
--   exact HasDerivAt.rpow (hasDerivAt_const x a) hf ha

lemma HasDerivAt.const_rpow {f : â„ â†’ â„} {f' a : â„} (ha : 0 < a) (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x => a ^ f x) ((Real.log a * f') * a ^ f x) x := by
  rw [(by norm_num; ring : (Real.log a * f') * a ^ f x = 0 * f x * a ^ (f x - 1) + f' * a ^ f x * Real.log a)]
  exact HasDerivAt.rpow (hasDerivAt_const x a) hf ha

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {f : E â†’ â„} {x : E} {f' : E â†’L[â„] â„}
  {s : Set E}

theorem DifferentiableWithinAt.logb (b : â„) (hf : DifferentiableWithinAt â„ f s x) (hx : f x â‰  0) :
    DifferentiableWithinAt â„ (fun x => logb b (f x)) s x := by
  unfold Real.logb
  fun_prop (disch := assumption)

@[fun_prop]
theorem DifferentiableAt.logb (b : â„) (hf : DifferentiableAt â„ f x) (hx : f x â‰  0) :
    DifferentiableAt â„ (fun x => logb b (f x)) x := by
  unfold Real.logb
  fun_prop (disch := assumption)

@[fun_prop]
theorem DifferentiableOn.logb (b : â„) (hf : DifferentiableOn â„ f s) (hx : âˆ€ x âˆˆ s, f x â‰  0) :
    DifferentiableOn â„ (fun x => logb b (f x)) s := fun x h => (hf x h).logb b (hx _ h)

theorem Differentiable.logb (hf : Differentiable â„ f) (hx : âˆ€ x, f x â‰  0) :
    Differentiable â„ fun x => logb b (f x) := fun x => (hf x).logb b (hx x)

end Diff

/- Some special limits -/

section Limits

open Real Filter Topology

lemma tendsto_x_mul_inv_x : Tendsto (fun x : â„ => x * xâ»Â¹) (ğ“[â‰ ] 0) (ğ“ 1) := by
  field_simp; apply tendsto_nhds_of_eventually_eq; apply eventually_nhdsWithin_of_forall
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff]
  intro x hx
  field_simp

-- Adapted from this proof: https://github.com/leanprover-community/mathlib4/blob/052d8d57c394373282ac1b581e828d9f3625e94c/Mathlib/Analysis/SpecialFunctions/Log/Deriv.lean#L208-L215
lemma tendsto_log_mul_inv_x (a : â„) : Tendsto (fun x : â„ => log (a * x + 1) * xâ»Â¹) (ğ“[â‰ ] 0) (ğ“ a) := by
  simpa [mul_comm, hasDerivAt_iff_tendsto_slope, slope_fun_def] using
     (((hasDerivAt_id (0 : â„)).const_mul a).add_const 1).log (by norm_num)

end Limits

/- Monotonicity of restricted function -/

lemma monotone_restrict [Preorder Î±] [Preorder Î²] {f : Î± â†’ Î²} {s : Set Î±} :
    Monotone (s.restrict f) â†” MonotoneOn f s := by simp [Set.restrict, Monotone, MonotoneOn]

lemma antitone_restrict [Preorder Î±] [Preorder Î²] {f : Î± â†’ Î²} {s : Set Î±} :
    Antitone (s.restrict f) â†” AntitoneOn f s := by simp [Set.restrict, Antitone, AntitoneOn]

/- Int.ceil lemmas -/

namespace Int

variable {F Î± Î² : Type*}
variable [LinearOrderedRing Î±] [FloorRing Î±] {z : â„¤} {a : Î±}

lemma ceil_nonpos (ha : a â‰¤ 0) : âŒˆaâŒ‰ â‰¤ 0 := by
  rw [ceil_le, cast_zero]; exact ha

lemma floor_eq_self : â†‘âŒŠaâŒ‹ = a â†” fract a = 0 := by
  simp only [fract, sub_eq_zero, eq_comm]

lemma ceil_eq_floor_iff_frac_eq_zero : âŒˆaâŒ‰ = âŒŠaâŒ‹ â†” fract a = 0 := by
  constructor <;> intro h
  Â· exact le_antisymm (sub_nonpos_of_le $ h â–¸ le_ceil _) (fract_nonneg a)
  Â· rw [ceil_eq_iff, floor_eq_self.mpr h]
    exact âŸ¨sub_one_lt _, le_refl _âŸ©

lemma ceil_eq_self : â†‘âŒˆaâŒ‰ = a â†” fract a = 0 := by
  constructor <;> intro h
  Â· rw [â† ceil_eq_floor_iff_frac_eq_zero]
    apply le_antisymm _ (floor_le_ceil a)
    rw [â† @Int.cast_le Î±, h, â†h, floor_intCast]
  Â· rw [ceil_eq_floor_iff_frac_eq_zero.mpr h, floor_eq_self.mpr h]

section LinearOrderedField

variable {k : Type*} [LinearOrderedField k] [FloorRing k] {b : k}

lemma ceil_div_mul_sub_lt {a b : k} (hb : 0 < b) : âŒˆa / bâŒ‰ * b - a < b := by
  rw [sub_lt_iff_lt_add, â†lt_div_iffâ‚€ hb, same_add_div (ne_of_gt hb), add_comm]
  exact ceil_lt_add_one _

lemma ceil_div_mul_sub_nonneg {a b : k} (hb : 0 < b) : 0 â‰¤ âŒˆa / bâŒ‰ * b - a := by
  rw [sub_nonneg, â† div_le_iffâ‚€ hb]
  exact le_ceil _

end LinearOrderedField

end Int
